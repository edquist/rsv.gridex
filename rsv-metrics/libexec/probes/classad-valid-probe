#!/usr/bin/env perl

use strict;
use warnings;
use RSVMetric;

print_output_header();

my %extra_opts = ("ress-collector=s" => "ReSS collector to use for validation of classads. Production: osg-ress-1.fnal.gov (default). ITB: osg-ress-4.fnal.gov");

my %options = process_command_line(%extra_opts);

my $collector = $options{'ress-collector'} || "osg-ress-1.fnal.gov";
my $glue_ce_info_host_name = $options{u};

find_binaries("condor_status");

# Want to get a list of the attributes whose names start with 'isClassadValid'
chomp(my @test_attrib_lines = qx{condor_status -l -pool $collector -constraint 'GlueCEInfoHostName == "$glue_ce_info_host_name"' | grep -E "^isClassadValid[^ ]* =" | sort | uniq});

if (! @test_attrib_lines) {
    print "CRITICAL\n";
    print "Cannot find CE host $glue_ce_info_host_name in ReSS collector $collector.\n";
    exit 0;
}

my $classad_valid_cmd = qq{condor_status -pool $collector -constraint 'GlueCEInfoHostName == "$glue_ce_info_host_name"' -format 'GlueSiteName=%s\n' GlueSiteName -format 'GlueCEInfoContactString=%s\n' GlueCEInfoContactString -format 'Name=%s\n' Name};
foreach my $attrib_line (@test_attrib_lines) {
    my ($attrib_name) = split ' ', $attrib_line;
    $classad_valid_cmd .= " -format '${attrib_name}=%d\\n' $attrib_name";
}

chomp(my @classad_array = `$classad_valid_cmd`);

if ($?) {
    print "UNKNOWN\n";
    print "Problem executing \"$classad_valid_cmd\"\n";
    print "Exited with status " . $? >> 8 . "\n";
    exit 0;
}
elsif (! @classad_array) {
    print "UNKNOWN\n";
    print "Problem executing \"$classad_valid_cmd\"\n";
    print "Command returned no output\n";
    exit 0;
}
else {
    if (! grep { /^isClassadValid=[01]$/ } @classad_array) {
        print "UNKNOWN\n";
        print "No value for isClassadValid attribute found\n";
        exit 0;
    }

    my $all_valid = ! grep { $_ eq 'isClassadValid=0' } @classad_array;
    if ($all_valid) {
        print "OK\n";
        print "ReSS Collector=$collector\n";
        print "Classad is valid\n";
        print join("\n", @classad_array)."\n";
        exit 0;
    }
    else {
        print "CRITICAL\n";
        print "ReSS Collector=$collector\n";
        print "Classad is invalid\n";
        print join("\n", @classad_array)."\n";
        exit 0;
    }
}



